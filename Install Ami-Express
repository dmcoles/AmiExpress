; Ami-Express
; $VER: Ami-Express 1.0 (09.08.2023)

(set #appver "v5.6.0")
(set #appyear "2018-2023")

(complete 0)
(set @app-name "Ami-Express")
(if  (exists "BBS:" (noreq))
  (set @default-dest "bbs:")
  (set @default-dest "sys:bbs")
)
; 

(if (< (getversion) (* 37 65536))
	(abort (cat
		"Ami-Express requires operating system "
		"version 2.04 or higher to work."
	))
)  

(welcome "Welcome to the Ami-Express installation utility.\nThis will install or upgrade your installation to AmiExpress " #appver)

  (set #muiver (/ (getversion "mui:mui" ) 65536) )

  (set #owndevunitver (/ (getversion "libs:owndevunit.library" ) 65536) )

  (set #fifolibver (/ (getversion "libs:fifo.library" ) 65536) )

  (set #amissllibver (/ (getversion "libs:amisslmaster.library" ) 65536) )
  (set #asynclibver (/ (getversion "libs:async.library" ) 65536) )

  (if ( <>  #muiver 0)
     ( set #muimsg "MUI: installed\n" )
     ( set #muimsg "MUI: not installed\n" )
  )

  (if ( <>  #owndevunitver 0)
     ( set #owndevunitmsg  "OwnDevUnit.library: installed\n" )
     ( set #owndevunitmsg "OwnDevUnit.library: not installed\n" )
  )
  
  (if ( <>  #fifolibver 0)
     ( set #fifolibmsg  "fifo library: installed\n" )
     ( set #fifolibmsg "fifo library: not installed\n" )
  )

  (if ( <>  #amissllibver 0)
     ( set #amissllibmsg  "amissl library: installed\n" )
     ( set #amissllibmsg "amissl library: not installed\n" )
  )

  (if ( <>  #asynclibver 0)
     ( set #asynclibmsg "async.library: installed\n" )
     ( set #asynclibmsg "async.library: not installed\n" )
  )

; 
( if (exists "sys:wbstartup/ACP" (noreq))
    ( set #acpname "sys:wbstartup/ACP")
    ( 
      ( if (exists "bbs:wbstartup/ACP" (noreq))
          ( set #acpname "bbs:wbstartup/ACP")
          ( set #acpname "")
      )
    )
)
;
(set expexists
  (exists "BBS:Express" (noreq))
)

; 
(if (= expexists 0)
(

; 
(message "After completion of the installation you will have a bare bones functioning bbs system installed and you will be able to use the included setup tool to customise the setup according to your preferences.\n\n(C)1989-91 Mike Thomas, Synthetic Technologies\n(C)1992-95 Joe Hodge, LightSpeed Technologies Inc.\n(C)" #appyear " Darren Coles\n\nFull documentation and the latest releases are always available at:\nhttps://github.com/dmcoles/AmiExpress\n"
)

; 
(set #default-dest-installergen-temp
  (askdir
    (prompt "Select where you want Ami-Express installed (create a folder if you need one)")
    (help "Select where you want Ami-Express installed.\nNo folder will be created so please ensure that you have created one if needed.")
    (default @default-dest)
  )
)
(set @default-dest #default-dest-installergen-temp)

; 
( complete 20)

(set #acpchoice (askchoice
      (choices "WBStartup (for auto startup)" "BBS: (for manual startup)" "Select a different location")
        (prompt "Where would you like to install the ACP program?")
        (help "")
        (default 0) ))

( if ( = #acpchoice 0)
    (set #acppath "sys:wbstartup") 
)

( if ( = #acpchoice 1)
    (set #acppath @default-dest) 
)

( if ( = #acpchoice 2)
  (
    (set #acppath
     (askdir
      (prompt "Please select the destination for the ACP file")
      (help #destdir-help)
      (default @default-dest) ) )
      )
)

(copyfiles
  (prompt "Copying Express")
  (help @copyfiles-help)
  (source "AmiExpress/BBS/Express")
  (dest @default-dest)
  (confirm )
)
( complete 30)

(copyfiles
  (source "AmiExpress/ACP")
  (dest #acppath)
  (prompt "Copying ACP")
  (help @copyfiles-help)
  (infos)
  (confirm "expert")

)

( complete 40)

(copyfiles
  (prompt "Copying utils")
  (help @copyfiles-help)
  (confirm "expert")
  (source "AmiExpress/BBS/Utils")
  (dest (tackon @default-dest "Utils"))
  (all)
)

( complete 45)

(copyfiles
  (prompt "Copying aedoor library")
  (help @copyfiles-help)
  (confirm "expert")
  (source "AmiExpress/libs")
  (dest "libs:")
  (all)
)

( complete 50)

(copyfiles
  (prompt "Copying extras")
  (help @copyfiles-help)
  (confirm "expert")
  (source "AmiExpress/c")
  (dest "c:")
  (all)
)

(copyfiles
  (prompt "Copying extras")
  (help @copyfiles-help)
  (confirm "expert")
  (source "AmiExpress/l")
  (dest "l:")
  (all)
)

( complete 55)

(copyfiles
  (prompt "Copying storage")
  (help @copyfiles-help)
  (confirm "expert")
  (source "AmiExpress/BBS/Storage")
  (dest (tackon @default-dest "Storage"))
  (all)
)
( complete 60)

; 
(copyfiles
  (prompt "Copying default bbs configuration")
  (help @copyfiles-help)
  (confirm "expert")
  (source "AmiExpress/defaultbbs")
  (dest @default-dest)
  (all)
)
( complete 70)

(run (cat "setenv axsetupeditor_prefs 11" (tackon #acppath "acp"))
  (prompt "Saving configuration")
  (help )
)

(run (cat "copy env:axsetupeditor_prefs envarc:")
  (prompt "Saving configuration")
  (help )
)

( complete 80)

(if  (exists "BBS:" (noreq))
   (set #startup "Assign doors: bbs:doors\nPath bbs:utils add\nResident SYS:Rexxc/RX pure")
   (set #startup (cat "Assign bbs: " @default-dest "\nAssign doors: bbs:doors\nPath bbs:utils add\nResident SYS:Rexxc/RX pure"))
)

 
(startup "Ami-Express"
  (prompt "Add startup items")
  (help @startup-help)
  (command #startup)
)
; 
 
) ; Else
(

; 
(set install-dest bbs:)

(run (cat "version full >env:tmpver " #acpname))
(set #acpver (getenv "tmpver") )
(if (= (substr #acpver 0 4) "ACP ")
  (set #acpver (substr #acpver 4 (- (strlen #acpver) 4))) 
)

(run (cat "version full >env:tmpver bbs:express"))
(set #expressver (getenv "tmpver") )
(if (= (substr #expressver 0 13) "Ami-Express ")
  (set #expressver (substr #expressver 13 (- (strlen #expressver) 13))) 
)

(message ("An existing installation of Ami-Express was found.\n\nThe currently installed version information is \n\nACP: %sExpress: %s\nPress proceed if you wish to continue upgrading Ami-Express.\n\nThe configuration of your bbs will not be affected and only the updated\napplication files will be overwritten.\n" #acpver #expressver) 
)

( complete 10)
; 

; 
( complete 20)

(copyfiles
  (source "AmiExpress/BBS/Express")
  (dest "bbs:")
  (prompt "Upgrading Express")
  (help @copyfiles-help)
  (confirm "expert")
)

( complete 40)

(set #acpdone 0)

(set #axprefs (getenv "axsetupeditor_prefs") )
(if (<> #axprefs "")
  (
   (set #axprefs (substr #axprefs 2 (- (strlen #axprefs) 5)))

    ( if (exists (tackon #axprefs "acp") (noreq))
      (
        (copyfiles
          (source "AmiExpress/ACP")
          (dest #axprefs)
          (prompt "Upgrading ACP")
          (help @copyfiles-help)
          (confirm "expert")
        )
        (set #acpdone 1)
      )
    ) 
  )
)
(message #axprefs)
( if (and (exists "sys:wbstartup/ACP" (noreq))
          (<> #axprefs "sys:wbstartup/") ) 
  (
    (copyfiles
      (source "AmiExpress/ACP")
      (dest "sys:wbstartup")
      (prompt "Upgrading ACP")
      (help @copyfiles-help)
      (confirm "expert")
    )
    (set #acpdone 1)
  )
)

( if (and (exists "bbs:acp" (noreq))
          (<> #axprefs "bbs:") ) 
  (
    (copyfiles
      (source "AmiExpress/ACP")
      (dest "bbs:")
      (prompt "Upgrading ACP")
      (help @copyfiles-help)
      (confirm "expert")
    )
    (set #acpdone 1)
  )
)

( if ( = #acpdone 0)
  (
    (set #acppath
     (askdir
      (prompt "Please select the location of your ACP file")
      (help #destdir-help)
      (default @default-dest) ) )

    ( if (exists (tackon #acppath "acp" ) (noreq))
      (
        (copyfiles
          (source "AmiExpress/ACP")
          (dest #acppath)
          (prompt "Upgrading ACP")
          (help @copyfiles-help)
          (confirm "expert")
        )
        ( set #acpdone 1 )
      )
    )
  )
)

( complete 60)

(copyfiles
  (prompt "Upgrading utils")
  (help @copyfiles-help)
  (confirm "expert")
  (source "AmiExpress/BBS/Utils")
  (dest "BBS:Utils")
  (pattern "~(rexxdoor)")
)

( complete 80)
; 
)
) ; End If

( message "The MUI system is required by the Ami-Express configuration tool and is recommended to be installed. In addition there are optional extras that can be used by Ami-Express which should be installed manually if required.\n\n" #muimsg #owndevunitmsg #fifolibmsg #amissllibmsg #asynclibmsg "\nThese extras are all freely available for download on the aminet.")

(exit)