; Ami-Express
; $VER: Ami-Express 1.0 (09.08.2023)

(set #appver "v5.6.0")
(set #appyear "2018-2023")

(complete 0)
(set @app-name "Ami-Express")
(if  (exists "BBS:" (noreq))
  (set @default-dest "bbs:")
  (set @default-dest "sys:bbs")
)
; 

(if (< (getversion) (* 37 65536))
	(abort (cat
		"Ami-Express requires operating system "
		"version 2.04 or higher to work."
	))
)  

(welcome "Welcome to the Ami-Express installation utility.\nThis will install or upgrade your installation to AmiExpress " #appver)

  (set #muiver (/ (getversion "mui:mui" ) 65536) )

  (set #owndevunitver (/ (getversion "libs:owndevunit.library" ) 65536) )

  (set #fifolibver (/ (getversion "libs:fifo.library" ) 65536) )

  (set #amissllibver (/ (getversion "libs:amisslmaster.library" ) 65536) )
  (set #asynclibver (/ (getversion "libs:async.library" ) 65536) )

  (if ( <>  #muiver 0)
     ( set #muimsg "MUI: installed\n" )
     ( set #muimsg "MUI: not installed\n" )
  )

  (if ( <>  #owndevunitver 0)
     ( set #owndevunitmsg  "OwnDevUnit.library: installed\n" )
     ( set #owndevunitmsg "OwnDevUnit.library: not installed\n" )
  )
  
  (if ( <>  #fifolibver 0)
     ( set #fifolibmsg  "fifo library: installed\n" )
     ( set #fifolibmsg "fifo library: not installed\n" )
  )

  (if ( <>  #amissllibver 0)
     ( set #amissllibmsg  "amissl library: installed\n" )
     ( set #amissllibmsg "amissl library: not installed\n" )
  )

  (if ( <>  #asynclibver 0)
     ( set #asynclibmsg "async.library: installed\n" )
     ( set #asynclibmsg "async.library: not installed\n" )
  )

; 
( if (exists "sys:wbstartup/ACP" (noreq))
    ( set #acpname "sys:wbstartup/ACP")
    ( 
      ( if (exists "bbs:wbstartup/ACP" (noreq))
          ( set #acpname "bbs:wbstartup/ACP")
          ( set #acpname "")
      )
    )
)
;
(set expexists
  (exists "BBS:Express" (noreq))
)

; 
(if (= expexists 0)
(

; 
(message "After completion of the installation you will have a bare bones functioning bbs system installed and you will be able to use the included setup tool to customise the setup according to your preferences.\n\n(C)1989-91 Mike Thomas, Synthetic Technologies\n(C)1992-95 Joe Hodge, LightSpeed Technologies Inc.\n(C)" #appyear " Darren Coles\n\nFull documentation and the latest releases are always available at:\nhttps://github.com/dmcoles/AmiExpress\n"
)

; 
(set #default-dest-installergen-temp
  (askdir
    (prompt "Select where you want Ami-Express installed (create a folder if you need one)")
    (help "Select where you want Ami-Express installed.\nNo folder will be created so please ensure that you have created one if needed.")
    (default @default-dest)
  )
)
(set @default-dest #default-dest-installergen-temp)

; 
( complete 20)

(set #acpchoice (askchoice
      (choices "WBStartup (for auto startup)" "BBS: (for manual startup)" "Select a different location")
        (prompt "Where would you like to install the ACP program?")
        (help "")
        (default 0) ))

( if ( = #acpchoice 0)
    (set #acppath "sys:wbstartup") 
)

( if ( = #acpchoice 1)
    (set #acppath @default-dest) 
)

( if ( = #acpchoice 2)
  (
    (set #acppath
     (askdir
      (prompt "Please select the destination for the ACP file")
      (help #destdir-help)
      (default @default-dest) ) )
      )
)

(copyfiles
  (prompt "Copying Express")
  (help @copyfiles-help)
  (source "AmiExpress/AmiExpress")
  (choices "Express")
  (dest @default-dest)
  (confirm "expert")
)
( complete 30)

(copyfiles
  (source "AmiExpress/AmiExpress")
  (dest #acppath)
  (prompt "Copying ACP")
  (help @copyfiles-help)
  (choices "ACP")
  (infos)
  (confirm "expert")

)

( complete 40)

(copyfiles
  (prompt "Copying utils")
  (help @copyfiles-help)
  (confirm "expert")
  (source "AmiExpress/AmiExpress/Utils")
  (dest (tackon @default-dest "Utils"))
  (all)
)

( complete 45)

(copyfiles
  (prompt "Copying icons")
  (help @copyfiles-help)
  (confirm "expert")
  (source "AmiExpress/AmiExpress/Storage")
  (dest (tackon @default-dest "Storage"))
  (all)
)


( complete 50)

(copylib
  (prompt "Copying aedoor library")
  (help @copylib-help)
  (confirm "average")
  (source "AmiExpress/libs/aedoor.library")
  (dest "libs:")
)

( complete 55)

(copyfiles
  (prompt "Copying archive tools")
  (help @copyfiles-help)
  (confirm "average")
  (source "AmiExpress/c")
  (dest "c:")
  (choices "LHA" "LZX" "ZIP" "UNZIP")
)

( complete 60)

(copyfiles
  (prompt "Copying LZX registration key")
  (help @copyfiles-help)
  (confirm "average")
  (source "AmiExpress/l")
  (dest "l:")
  (choices "LZX.keyfile")
)

( complete 65)

(copyfiles
  (prompt "Copying file id tools")
  (help @copyfiles-help)
  (confirm "expert")
  (source "AmiExpress/fileid")
  (dest "c:")
  (choices "Textract" "DMSDescript" "EXEDescript" "GIFDesc")
)

( complete 70)

; 
(copyfiles
  (prompt "Copying default bbs configuration")
  (help @copyfiles-help)
  (confirm "expert")
  (source "AmiExpress/defaultbbs")
  (dest @default-dest)
  (all)
)
( complete 90)

(run (cat "setenv axsetupeditor_prefs 11" (tackon #acppath "acp"))
  (prompt "Saving configuration")
  (help )
)

(run (cat "copy env:axsetupeditor_prefs envarc:")
  (prompt "Saving configuration")
  (help )
)

( complete 95)

(if  (exists "BBS:" (noreq))
   (set #startup "Assign doors: bbs:doors\nPath bbs:utils add\nResident SYS:Rexxc/RX pure")
   (set #startup (cat "Assign bbs: " @default-dest "\nAssign doors: bbs:doors\nPath bbs:utils add\nResident SYS:Rexxc/RX pure"))
)

 
(startup "Ami-Express"
  (prompt "Add startup items")
  (help @startup-help)
  (command #startup)
)

( message "The user password encryption used by Ami-Express prior to this release is extremely weak and easily hackable using brute force.\n\nThis release introduces password encryption using PKBDF2. The number of rounds of PKBDF2 has been set to 100 which is a compromise between security and the processing capability of the Amiga. If you have a powerful CPU you should consider changing the settings to make it more secure.")

; 
 
) ; Else
(

; 
(set install-dest bbs:)

(if (exists "env:axsetupeditor_prefs" (noreq))
  (set #axprefs (getenv "axsetupeditor_prefs") )
  (set #axprefs "")
)

(set #acppath "")
(if (<> #axprefs "")
  (
   (set #axprefs (substr #axprefs 2 (- (strlen #axprefs) 5)))

    ( if (exists (tackon #axprefs "acp") (noreq))
      (set #acppath #axprefs)
    )
  )
)
(if (= #acppath "")
  (if (exists "sys:wbstartup/ACP" (noreq))
      (set #acppath "sys:wbstartup")
  )
)
(if (= #acppath "")
  (if (exists "bbs:ACP" (noreq))
      (set #acppath "bbs:")
  )
)

(if (= #acppath "")
  (set #acppath
   (askdir
    (prompt "Please select the location of your ACP file")
    (help #destdir-help)
    (default @default-dest) ) )
)
   

( complete 10)

(set #acpname (tackon #acppath "acp"))

(if  (= #acpname "")
  (set #acpver "Unknown\n")
  (
    (run (cat "echo >ram:out " #acpname))
    (run (cat "version full >env:tmpver \"" #acpname "\""))
    (set #acpver (getenv "tmpver") )
    (if (= (substr #acpver 0 4) "ACP ")
      (set #acpver (substr #acpver 4 (- (strlen #acpver) 4))) 
    )
  )
)

(run (cat "version full >env:tmpver bbs:express"))
(set #expressver (getenv "tmpver") )
(if (= (substr #expressver 0 11) "AmiExpress ")
  (set #expressver (substr #expressver 11 (- (strlen #expressver) 11))) 
)

(message ("An existing installation of Ami-Express was found.\n\nThe currently installed version information is \n\nACP: %sExpress: %s\nPress proceed if you wish to continue upgrading Ami-Express.\n\nThe configuration of your bbs will not be affected and only the updated\napplication files will be overwritten.\n" #acpver #expressver) 
)

( complete 20)

(copyfiles
  (source "AmiExpress/AmiExpress")
  (dest "bbs:")
  (prompt "Upgrading Express")
  (choices "Express")
  (help @copyfiles-help)
  (confirm "expert")
)

( complete 40)

( if (exists (tackon #acppath "ACP" (noreq)))
    (copyfiles
      (source "AmiExpress/AmiExpress")
      (dest #acppath)
      (prompt "Upgrading ACP")
      (help @copyfiles-help)
      (choices "ACP")
      (confirm "expert")
    )
)

( complete 50)

(if (= (exists "env:axsetupeditor_prefs" (noreq)) 0)
  (
    (run (cat "setenv axsetupeditor_prefs 11" (tackon #acppath "acp"))
      (prompt "Saving configuration")
      (help )
    )

    (run (cat "copy env:axsetupeditor_prefs envarc:")
      (prompt "Saving configuration")
      (help )
    )
  )
)

( complete 60)

(copyfiles
  (prompt "Upgrading utils")
  (help @copyfiles-help)
  (confirm "expert")
  (source "AmiExpress/AmiExpress/Utils")
  (dest "BBS:Utils")
  (pattern "~(rexxdoor)")
)

( complete 80)

(set #currsecurity "")
(iconinfo (dest "bbs:bbsconfig") (gettooltype "PASSWORD_SECURITY" "#currsecurity" ))

( if ( = #currsecurity "")
(
  (set #securitychoice (askchoice
        (choices "Enable the new encryption" "Keep the legacy encryption"
          (prompt "This release of Ami-Express includes a new password encryption system.\nThe old system was extremely weak and easily hacked using brute force.\nIf you have to downgrade Ami-Express for any reason, new users created with the new password security will not be able to log into the BBS.")
          (help "")
          (default 0) ))
  )

  (if (= (exists "bbs:bbsconfig.info" (noreq)) 0)
    (
      (copyfiles
        (prompt "Creating bbsconfig.info")
        (help @copyfiles-help)
        (source "AmiExpress/defaultbbs/bbsconfig.info")
        (dest "BBS:")
        )
      
      (tooltype (dest "bbs:bbsconfig") (settooltype "HISTORY"))
      (tooltype (dest "bbs:bbsconfig") (settooltype "CONVERT_TO_MB"))
     )

  )


    ( if ( = #securitychoice 0)
       (tooltype (dest "bbs:bbsconfig") (settooltype "PASSWORD_SECURITY" "PKBDF2_100"))
       (tooltype (dest "bbs:bbsconfig") (settooltype "PASSWORD_SECURITY" "LEGACY"))
    )
  )
)


( complete 90)

; 
)
) ; End If

( message "The MUI system is required by the Ami-Express configuration tool and is recommended to be installed. In addition there are optional extras that can be used by Ami-Express which should be installed manually if required.\n\n" #muimsg #owndevunitmsg #fifolibmsg #amissllibmsg #asynclibmsg "\nThese extras are all freely available for download on the aminet.")

(exit)